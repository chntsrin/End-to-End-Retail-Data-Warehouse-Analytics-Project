/*
========================================================================================================================
Load Silver tables (Bronze -> Silver)
========================================================================================================================
Script Purpose:
Truncate to clear all data in tables if it already exist and then use Bulk Insert to load cleaned data to Silver tables
*/


DECLARE @start_time DATETIME, @end_time DATETIME
SET @start_time = GETDATE();
PRINT('Start Loading...')


-- Load cleaned data from bronze.crm_cust_info to silver.crm_cust_info
PRINT('Loading cleaned data to silver.crm_cust_info')
TRUNCATE TABLE silver.crm_cust_info
INSERT INTO silver.crm_cust_info (
	cst_id,
	cst_key,
	cst_firstname,
	cst_lastname,
	cst_marital,
	cst_gender,
	cst_create_date)

SELECT cst_id,
	   cst_key,
	   TRIM(cst_firstname) as cst_firstname,
	   TRIM(cst_lastname) as cst_lastname,
	   CASE WHEN TRIM(UPPER(cst_marital)) = 'M' THEN 'Married'
	        WHEN TRIM(UPPER(cst_marital)) = 'S' THEN 'Single'
			ELSE 'n/a'
			END AS cst_marital,
	   CASE WHEN TRIM(UPPER(cst_gender)) = 'F' THEN 'Female'
	        WHEN TRIM(UPPER(cst_gender)) = 'M' THEN 'Male'
			ELSE 'n/a'
			END AS cst_gender,
	   cst_create_date
FROM (SELECT *, ROW_NUMBER() OVER(PARTITION BY cst_id ORDER BY cst_create_date DESC) as Ranking
	  FROM bronze.crm_cust_info
	  WHERE cst_id IS NOT NULL) as t
WHERE ranking = 1

PRINT('Load cleaned data to silver.crm_cust_info successfully!')
PRINT('')


-- Load cleaned data from bronze.crm_cust_info to silver.crm_prd_info
PRINT('Loading cleaned data to silver.crm_prd_info')
TRUNCATE TABLE silver.crm_prd_info
INSERT INTO silver.crm_prd_info (
	prd_id,
	cat_id,
	prd_key,
	prd_nm,
	prd_cost,
	prd_line,
	prd_start_dt,
	prd_end_dt)

SELECT prd_id,
	   REPLACE(SUBSTRING(prd_key,1,5),'-','_') as cat_id,
	   SUBSTRING(prd_key,7,LEN(prd_key)) as prd_key,
	   prd_nm,
	   ISNULL(prd_cost,0) as prd_cost,
	   CASE WHEN UPPER(TRIM(prd_line)) = 'M' THEN 'Mountain'
	        WHEN UPPER(TRIM(prd_line)) = 'R' THEN 'Road'
			WHEN UPPER(TRIM(prd_line)) = 'S' THEN 'Other Sales'
			WHEN UPPER(TRIM(prd_line)) = 'T' THEN 'Touring'
			ELSE 'n/a'
			END AS prd_line,
	   CAST(prd_start_dt AS DATE) as prd_start_dt,
	   CAST(DATEADD(day,-1,LEAD(prd_start_dt) OVER(PARTITION BY prd_key ORDER BY prd_start_dt)) AS DATE) as prd_end_dt
FROM bronze.crm_prd_info

PRINT('Load cleaned data successfully!')
PRINT('')


-- Load cleaned data from bronze.crm_sales_detaisls to silver.crm_sales_details
PRINT('Loading cleaned data to silver.crm_sales_details')
TRUNCATE TABLE silver.crm_sales_details
INSERT INTO silver.crm_sales_details (
	sls_ord_num,
	sls_prd_key,
	sls_cust_id,
	sls_order_dt,
	sls_ship_dt,
	sls_due_dt,
	sls_sales,
	sls_quantity,
	sls_price)

SELECT sls_ord_num,
	   sls_prd_key,
	   sls_cust_id,
	   CASE WHEN sls_order_dt <= 0 OR LEN(sls_order_dt) != 8 THEN NULL
		    ELSE CAST(CAST(sls_order_dt AS VARCHAR) AS DATE)
	   END AS sls_order_dt,
	   CASE WHEN sls_ship_dt <= 0 OR LEN(sls_ship_dt) != 8 THEN NULL
		    ELSE CAST(CAST(sls_ship_dt AS VARCHAR) AS DATE)
	   END AS sls_ship_dt,
	   CASE WHEN sls_due_dt <= 0 OR LEN(sls_due_dt) != 8 THEN NULL
	        ELSE CAST(CAST(sls_due_dt AS VARCHAR) AS DATE)
	   END AS sls_due_dt,
	   CASE WHEN sls_sales <= 0 OR sls_sales IS NULL OR sls_sales != sls_quantity * ABS(sls_price) THEN sls_quantity * ABS(sls_price)
			ELSE sls_sales
	   END AS sls_sales,
	   sls_quantity,
	   CASE WHEN sls_price = 0 OR sls_price IS NULL THEN sls_sales/NULLIF(sls_quantity,0)
			WHEN sls_price < 0 THEN ABS(sls_price)
			ELSE sls_price
	   END AS sls_price
FROM bronze.crm_sales_details

PRINT('Load cleaned data successfully!')
PRINT('')


-- Load cleaned data from bronze.erp_cust_az12 to silver.erp_cust_az12
PRINT('Loading cleaned data to silver.erp_cust_az12')
TRUNCATE TABLE silver.erp_cust_az12
INSERT INTO silver.erp_cust_az12(
	cid,
	bdate,
	gen)

SELECT REPLACE(cid,'NAS','') as cid,
	   CASE WHEN bdate > GETDATE() THEN NULL
		    ELSE bdate
	   END AS bdate,
	   CASE WHEN UPPER(TRIM(gen)) IN ('F','FEMALE') THEN 'Female'
			WHEN UPPER(TRIM(gen)) IN ('M','Male') THEN 'Male'
			ELSE 'n/a'
	   END AS gen
FROM bronze.erp_cust_az12

PRINT('Load cleaned data successfully!')
PRINT('')


-- Load cleaned data from bronze.erp_loc_a101 to silver.erp_loc_a101
PRINT('Loading cleaned data to silver.erp_loc_a101')
TRUNCATE TABLE silver.erp_loc_a101
INSERT INTO silver.erp_loc_a101(
	cid,
	cntry)

SELECT REPLACE(cid,'-','') as cid,
	   CASE WHEN TRIM(cntry) = 'DE' THEN 'Germany'
			WHEN TRIM(cntry) IN ('US','USA') THEN 'United States'
			WHEN TRIM(cntry) = '' OR cntry IS NULL THEN 'n/a'
			ELSE TRIM(cntry)
	   END AS cntry
FROM bronze.erp_loc_a101

PRINT('Load cleaned data successfully!')
PRINT('')


-- Load cleaned data from bronze.erp_px_cat_g1v2 to silver.erp_px_cat_g1v2
PRINT('Loading cleaned data to silver.erp_px_cat_g1v2')
TRUNCATE TABLE silver.erp_px_cat_g1v2
INSERT INTO  silver.erp_px_cat_g1v2(
	id,
	cat,
	subcat,
	maintenance)

SELECT id,
	   cat,
	   subcat,
	   maintenance
FROM bronze.erp_px_cat_g1v2

PRINT('Load cleaned data successfully!')
PRINT('')


SET @end_time = GETDATE();
PRINT('Loading Time: ' + CAST(DATEDIFF(second,@start_time,@end_time) AS NVARCHAR) + ' seconds.')